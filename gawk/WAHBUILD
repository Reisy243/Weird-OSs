pkgname='gawk'
pkgver='5.3.0'
pkgrel=1
pkgdesc='GNU version of awk'
arch=('x86_64')
url="https://www.gnu.org/software/$pkgname"
license=('GPL')
depends=('sh' 'libc' 'mpfr')
provides=('awk')
source=("https://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig})
sha256sums=(
	'378f8864ec21cfceaa048f7e1869ac9b4597b449087caf1eb55e440d30273336'	# gawk-5.3.0.tar.gz
	'SKIP'									# gawk-5.3.0.tar.gz.sig
)
validpgpkeys=('D1967C63788713177D861ED7DF597815937EC0D2')	# Arnold Robbins

build() {
	cd $pkgname-$pkgver
	./configure --prefix=/usr --libexecdir=/usr/lib --sysconfdir=/etc --without-libsigsegv
	make
}

check() {
	make DESTDIR="$pkgdir" -C $pkgname-$pkgver check
}

package() {
	make DESTDIR="$pkgdir" -C $pkgname-$pkgver install
}
