pkgmaintainer='Isaac David Orozco Delgado (Reisy RedPanda) <opensrcrdp@gmail.com>'
pkgrepo='System'

pkgname='file'
pkgver='5.45'
pkgrel=1
pkgdesc='File type identification utility'
arch=('x86_64')
license=('custom')
url="https://www.darwinsys.com/$pkgname"
depends=('libc' 'zlib' 'xz' 'bzip2' 'libseccomp'{,.so} 'zstd' 'libzstd.so')
provides=('libmagic.so')
options=('!emptydirs')
source=("https://astron.com/pub/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
sha256sums=(
	'fc97f51029bb0e2c9f4e3bffefdaf678f0e039ee872b9de5c002a6d09c784d82'	# file-5.45.tar.gz
	'SKIP'									# file-5.45.tar.gz.asc
)
validpgpkeys=('BE04995BA8F90ED0C0C176C471112AB16CB33B3A') # Christos Zoulas

build() {
	cd $pkgname-$pkgver

	# Fix linking libmagic (vfork needs libpthread)
	CFLAGS+=" -pthread"

	./configure \
		--prefix=/usr \
		--datadir=/usr/share/file \
		--enable-fsect-man5 \
		--enable-libseccomp

	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

check() {
	make -C $pkgname-$pkgver check
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir" install
	install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
