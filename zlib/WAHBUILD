pkgmaintainer='Isaac David Orozco Delgado (Reisy RedPanda) <opensrcrdp@gmail.com>'
pkgrepo='System'

pkgbase='zlib'
pkgname=($pkgbase minizip)
pkgver='1.3.1'
pkgrel=1
pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP'
arch=('x86_64')
license=('Zlib')
url="https://www.$pkgbase.net/"
options=(
	staticlibs # needed by binutils testsuite
	debug
)
source=("https://github.com/madler/$pkgbase/releases/download/v$pkgver/$pkgbase-$pkgver.tar.xz"{,.asc})
sha256sums=(
	'38ef96b8dfe510d42707d9c781877914792541133e1870841463bfa73f883e32'	# zlib-1.3.1.tar.xz
	'SKIP'									# zlib-1.3.1.tar.xz,asc
)
sha512sums=(
	'1e8e70b362d64a233591906a1f50b59001db04ca14aaffad522198b04680be501736e7d536b4191e2f99767e7001ca486cd802362cca2be05d5d409b83ea732d'	# zlib-1.3.1.tar.xz
	'SKIP'																	# zlib-1.3.1.tar.xz.asc
)
b2sums=(
	'42d109223801a493de6d52e7343403d7fc3234a6ca816425fe41ac9c18019b01b93841acd28a235e99f2256a6a17f93624e96b2ddb58d588c8190a6bedb82910'	# zlib-1.3.1.tar.xz
	'SKIP'																	# zlib-1.3.1.tar.xz.asc
)
validpgpkeys=('5ED46A6721D365587791E2AA783FCD8E58BCAFBA')	# Mark Adler <madler@alumni.caltech.edu>

prepare() {
	cd $pkgbase-$pkgver/contrib/minizip
	cp Makefile Makefile.orig
	cp ../README.contrib readme.txt
	autoreconf -fiv
}

build() {
	cd $pkgbase-$pkgver
	CFLAGS+=" -ffat-lto-objects"
	./configure --prefix=/usr
	make

	cd contrib/minizip
	./configure --prefix=/usr --enable-static=no
	make
}

check() {
	make test -C $pkgbase-$pkgver
	make -f Makefile.orig test -C $pkgbase-$pkgver/contrib/minizip
}

package_zlib() {
	depends=('libc.so')
	provides=('libz.so')

	cd $pkgbase-$pkgver
	make install DESTDIR="$pkgdir"
	install -D -m644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}

package_minizip() {
	pkgdesc='Mini zip and unzip based on zlib'
	depends=("libc.so" "$pkgbase")

	cd $pkgbase-$pkgver
	make install DESTDIR="$pkgdir" -C contrib/$pkgname
	install -D -m644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"

	# https://github.com/madler/zlib/pull/229
	rm "$pkgdir/usr/include/minizip/crypt.h"
}
