pkgmaintainer='Isaac David Orozco Delgado (Reisy RedPanda) <opensrcrdp@gmail.com>'
pkgrepo='System'

pkgname='ncurses'
pkgver='6.5'
pkgrel=1
pkgdesc='System V Release 4.0 curses emulation library'
arch=('x86_64')
url="https://invisible-island.net/$pkgname/$pkgname.html"
srcurl="https://raw.githubusercontent.com/Reisy243/Weird-OSs/panwah/$pkgname"
license=('MIT-open-group')
depends=('libc' 'gcc-libs')
makedepends=('autoconf-archive')
provides=(lib{ncurses{,++},form,menu,panel}w.so)
optdepends=('bash: for ncursesw6-config')
replaces=({alacritty,rio,wezterm}-terminfo)
source=(
	"https://invisible-island.net/archives/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc}
	"$srcurl/$pkgname-6.3"-{libs,pkgconfig}.patch
)
sha256sums=(
	'136d91bc269a9a5785e5f9e980bc76ab57428f604ce3e5a5a90cebc767971cc6'	# ncurses-6.5.tar.gz
	'SKIP'									# ncurses-6.5.tar.gz.asc
	'dc4261b6642058a9df1c0945e2409b24f84673ddc3a665d8a15ed3580e51ee25'	# ncurses-6.3-libs.patch
	'b8544a607dfbeffaba2b087f03b57ed1fa81286afca25df65f61b04b5f3b3738'	# ncurses-6.3-pkgconfig.patch
)
sha512sums=(
	'fc5a13409d2a530a1325776dcce3a99127ddc2c03999cfeb0065d0eee2d68456274fb1c7b3cc99c1937bc657d0e7fca97016e147f93c7821b5a4a6837db821e8'	# ncurses-6.5.tar.gz
	'SKIP'																	# ncurses-6.5.tar.gz.asc
	'adb02b838c40f1e58a1b31c26d5cd0f2a1c43f3b6d68e839981764c0f6c905a9eb51dd36ff018628fdeb20747cc7467727d57135408ab4848259384077a52b28'	# ncurses-6.3-libs.patch
	'2d2c0ec3c880e638ab4aa3dbff5e28e4cd233153e24816bd87e077f848aa3edd5114cd0f2a7f6e8869dd1861a2746e512886c18264ff1676927dcc320c5ef958'	# ncurses-6.3-pkgconfig.patch
)
b2sums=(
	'd0a05c218b5f320ba9abe2f322787baef1d15d764e503c81d49772b54b2927d28ecbeb500c8e514996226b41f69445b4de44ff423cec25b61847307fe61682ec'	# ncurses-6.5.tar.gz
	'SKIP'																	# ncurses-6.5.tar.gz.asc
	'31bb10e82dd018a75e57252052650d9f0f5eb5e7e887118c2ea40032b11f59ec6aa4d9bae804c615cbecdf3382f3434e0c9e9e8440fdefe66a507be020b8965c'	# ncurses-6.3-libs.patch
	'fb6cf606cf3db7f6b306272696a63bce83d52cfa91e850f9a7bdb9d3d8455a26943529a9cf79731dddc7f763c27211a9afab9c4c31dbb6d12fd720eb390eb0a3'	# ncurses-6.3-pkgconfig.patch
)
validpgpkeys=('19882D92DDA4C400C22C0D56CC2AF4472167BE03')	# Thomas Dickey <dickey@invisible-island.net>

prepare() {
	# do not link against test libraries
	patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-6.3-libs.patch
	# do not leak build-time LDFLAGS into the pkgconfig files:
	# https://bugs.archlinux.org/task/68523
	patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-6.3-pkgconfig.patch
	# NOTE: can't run autoreconf because the autotools setup is custom and ancient
}

build() {
	cd $pkgname-$pkgver

	./configure \
		--prefix=/usr \
		--disable-root-access \
		--disable-root-environ \
		--disable-setuid-environ \
		--enable-widec \
		--enable-pc-files \
		--mandir=/usr/share/man \
		--with-cxx-binding \
		--with-cxx-shared \
		--with-manpage-format=normal \
		--with-pkg-config-libdir=/usr/lib/pkgconfig \
		--with-shared \
		--with-versioned-syms \
		--with-xterm-kbs=del \
		--without-ada

	make
}

package(){
	make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
	install -Dm 644 $pkgname-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"

	# fool packages looking to link to non-wide-character ncurses libraries
	for lib in ncurses ncurses++ form panel menu; do
		printf "INPUT(-l%sw)\n" "${lib}" > "$pkgdir/usr/lib/lib${lib}.so"
		ln -s ${lib}w.pc "$pkgdir/usr/lib/pkgconfig/${lib}.pc"
	done

	# some packages look for -lcurses during build
	printf 'INPUT(-lncursesw)\n' > "$pkgdir/usr/lib/libcursesw.so"
	ln -s libncurses.so "$pkgdir/usr/lib/libcurses.so"

	# tic and ticinfo functionality is built in by default
	# make sure that anything linking against it links against libncursesw.so instead
	for lib in tic tinfo; do
		printf "INPUT(libncursesw.so.%s)\n" "${pkgver:0:1}" > "$pkgdir/usr/lib/lib${lib}.so"
		ln -s libncursesw.so.${pkgver:0:1} "$pkgdir/usr/lib/lib${lib}.so.${pkgver:0:1}"
		ln -s ncursesw.pc "$pkgdir/usr/lib/pkgconfig/${lib}.pc"
	done
}
