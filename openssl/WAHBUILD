pkgmaintainer='Isaac David Orozco Delgado (Reisy RedPanda) <opensrcrdp@gmail.com>'
pkgrepo='System'

pkgname='openssl'
pkgver='3.3.1'
pkgrel=1
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security'
arch=('x86_64')
url="https://www.$pkgname.org"
srcurl="https://raw.githubusercontent.com/Reisy243/Weird-OSs/panwah/$pkgname"
license=('Apache-2.0')
depends=('libc')
makedepends=('perl')
optdepends=('ca-certificates' 'perl')
replaces=('openssl-perl' 'openssl-doc')
provides=(lib{crypto,ssl}.so)
backup=("etc/ssl/$pkgname.cnf")
source=(
	"$url/source/$pkgname-$pkgver.tar.gz"{,.asc}
	"$srcurl/ca-dir.patch"
)
sha256sums=(
	'777cd596284c883375a2a7a11bf5d2786fc5413255efab20c50d6ffe6d020b7e'	# openssl-3.3.1.tar.gz
	'SKIP'									# openssl-3.3.1.tar.gz.asc
	'0a32d9ca68e8d985ce0bfef6a4c20b46675e06178cc2d0bf6d91bd6865d648b7'	# ca-dir.patch
)
validpgpkeys=(
	'7953AC1FBC3DC8B3B292393ED5E9E43F7DF9EE8C'	# Richard Levitte <richard@levitte.org>
	'8657ABB260F056B1E5190839D9C4D26D0E604491'	# Matt Caswell <matt@openssl.org>
	'A21FAB74B0088AA361152586B8EF1A6BA9DA2D5C'	# Tomáš Mráz <tm@t8m.info>
	'EFC0A467D613CB83C7ED6D30D894E2CE8B3D79F5'	# OpenSSL security team <openssl-security@openssl.org>
)

prepare(){
	cd $pkgname-$pkgver

	# set ca dir to /etc/ssl by default
	patch -Np1 -i "$srcdir/ca-dir.patch"
}

build(){
	cd $pkgname-$pkgver

	./Configure --prefix=/usr \
		--openssldir=/etc/ssl \
		--libdir=lib \
		shared \
		enable-ktls \
		enable-ec_nistp_64_gcc_128 \
		linux-$TARCH

	make depend
	make
}

check(){
	cd $pkgname-$pkgver

	# the test fails due to missing write permissions in /etc/ssl
	# revert this patch for make test
	patch -Rp1 -i "$srcdir/ca-dir.patch"

	make HARNESS_JOBS=$(nproc) test

	patch -Np1 -i "$srcdir/ca-dir.patch"
	# re-run make to re-generate CA.pl from the patched .in file.
	make apps/CA.pl
}

package(){
	cd $pkgname-$pkgver

	make DESTDIR="$pkgdir" MANDIR=/usr/share/man MANSUFFIX=ssl install_sw install_ssldirs install_man_docs
	install -D -m644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
